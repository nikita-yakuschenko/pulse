Функция GetSOrders(Запрос)
    Попытка
        МассивЗаказовПоставщикам = Новый Массив;
        
        // Параметры: /get/full/{full}, /get/code/{code}, /get/contractor/{contractor}, /get/year/{year}
        ПолныйРежим = Ложь;
        ПолныйПараметр = "";
        КодФильтр = "";
        КонтрагентФильтр = "";
        ГодФильтр = "";
        Попытка
            ПолныйПараметр = НРег(СокрЛП(Строка(Запрос.ПараметрыURL["full"])));
        Исключение
            ПолныйПараметр = "";
        КонецПопытки;
        Если Не ПустаяСтрока(ПолныйПараметр) Тогда
            Если ПолныйПараметр = "1"
                Или ПолныйПараметр = "true"
                Или ПолныйПараметр = "yes"
                Или ПолныйПараметр = "full" Тогда
                ПолныйРежим = Истина;
            КонецЕсли;
        КонецЕсли;
        
        Попытка
            КодФильтр = СокрЛП(Строка(Запрос.ПараметрыURL["code"]));
        Исключение
            КодФильтр = "";
        КонецПопытки;
        Попытка
            КонтрагентФильтр = СокрЛП(Строка(Запрос.ПараметрыURL["contractor"]));
        Исключение
            КонтрагентФильтр = "";
        КонецПопытки;
        // Обработка спец. маркера для "+" в path
        Если Не ПустаяСтрока(КонтрагентФильтр) Тогда
            // В GET path IIS режет "+", поэтому используем замену "_plus_" -> "+"
            КонтрагентФильтр = СтрЗаменить(КонтрагентФильтр, "_plus_", "+");
            // Поддержка дефиса/минуса: "_dash_" или "_minus_" -> "-"
            КонтрагентФильтр = СтрЗаменить(КонтрагентФильтр, "_dash_", "-");
            КонтрагентФильтр = СтрЗаменить(КонтрагентФильтр, "_minus_", "-");
        КонецЕсли;
        Попытка
            ГодФильтр = СокрЛП(Строка(Запрос.ПараметрыURL["year"]));
        Исключение
            ГодФильтр = "";
        КонецПопытки;
        
        Если ПолныйРежим И ПустаяСтрока(КодФильтр) И ПустаяСтрока(КонтрагентФильтр) Тогда
            Ответ = Новый HTTPСервисОтвет(400);
            Ответ.Заголовки.Вставить("Content-Type", "text/plain; charset=utf-8");
            Ответ.УстановитьТелоИзСтроки("full требует фильтр: code или contractor");
            Возврат Ответ;
        КонецЕсли;
        
        УсловиеГде = "";
        Если Не ПустаяСтрока(КодФильтр) Тогда
            УсловиеГде = "ЗаказПоставщику.Номер ПОДОБНО &КодФильтр";
        КонецЕсли;
        Если Не ПустаяСтрока(КонтрагентФильтр) Тогда
            УсловиеГде = УсловиеГде + ?(ПустаяСтрока(УсловиеГде), "", " И ")
                + "ЗаказПоставщику.Контрагент.Наименование ПОДОБНО &КонтрагентФильтр";
        КонецЕсли;
        Если Не ПустаяСтрока(ГодФильтр) Тогда
            УсловиеГде = УсловиеГде + ?(ПустаяСтрока(УсловиеГде), "", " И ")
                + "ЗаказПоставщику.Дата МЕЖДУ &ДатаНач И &ДатаКон";
        КонецЕсли;
        
        ЗапросБД = Новый Запрос;
        ЗапросБД.Текст = 
            "ВЫБРАТЬ
            |	ЗаказПоставщику.Ссылка КАК Ссылка,
            |	ЗаказПоставщику.Номер КАК Номер,
            |	ЗаказПоставщику.Дата КАК Дата,
            |	ЗаказПоставщику.Контрагент.Наименование КАК Контрагент,
            |	ЗаказПоставщику.Организация.Наименование КАК Организация,
            |	ЗаказПоставщику.СуммаДокумента КАК Сумма,
            |	ЗаказПоставщику.Ответственный.Наименование КАК Ответственный,
            |	ХранилищеДополнительнойИнформации.Ссылка КАК Ссылка1,
            |	ХранилищеДополнительнойИнформации.Хранилище,
            |	ХранилищеДополнительнойИнформации.ТекстФайла,
            |	ХранилищеДополнительнойИнформации.Представление,
            |	ХранилищеДополнительнойИнформации.Объект,
            |	ХранилищеДополнительнойИнформации.Объект КАК Объект1
            |ИЗ
            |	Справочник.ХранилищеДополнительнойИнформации КАК ХранилищеДополнительнойИнформации
            |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику КАК ЗаказПоставщику
            |		ПО ХранилищеДополнительнойИнформации.Объект = ЗаказПоставщику.Ссылка"
            + ?(ПустаяСтрока(УсловиеГде), "", "
            |ГДЕ
            |   " + УсловиеГде);
        
        Если Не ПустаяСтрока(КодФильтр) Тогда
            ЗапросБД.УстановитьПараметр("КодФильтр", "%" + КодФильтр + "%");
        КонецЕсли;
        Если Не ПустаяСтрока(КонтрагентФильтр) Тогда
            ЗапросБД.УстановитьПараметр("КонтрагентФильтр", "%" + КонтрагентФильтр + "%");
        КонецЕсли;
        Если Не ПустаяСтрока(ГодФильтр) Тогда
            ГодФильтрЧисло = Число(ГодФильтр);
            Если СтрДлина(ГодФильтр) = 2 Тогда
                ГодФильтрЧисло = 2000 + ГодФильтрЧисло;
            КонецЕсли;
            ЗапросБД.УстановитьПараметр("ДатаНач", Дата(ГодФильтрЧисло, 1, 1));
            ЗапросБД.УстановитьПараметр("ДатаКон", Дата(ГодФильтрЧисло, 12, 31, 23, 59, 59));
        КонецЕсли;

        Результат = ЗапросБД.Выполнить();
        Выборка = Результат.Выбрать();

        Пока Выборка.Следующий() Цикл
            ЗаказПоставщику = Новый Структура;
            
            Если Выборка.Дата = Неопределено Тогда
                ЗаказПоставщику.Вставить("Дата", "");
            Иначе
                ЗаказПоставщику.Вставить("Дата", Строка(Выборка.Дата));
            КонецЕсли;
            
            Если Выборка.Номер = Неопределено Тогда
                ЗаказПоставщику.Вставить("Номер", "");
            Иначе
                ЗаказПоставщику.Вставить("Номер", Строка(Выборка.Номер));
            КонецЕсли;
            
            Если Выборка.Контрагент = Неопределено Тогда
                ЗаказПоставщику.Вставить("Контрагент", "");
            Иначе
                ЗаказПоставщику.Вставить("Контрагент", Строка(Выборка.Контрагент));
            КонецЕсли;
            
            Если Выборка.Организация = Неопределено Тогда
                ЗаказПоставщику.Вставить("Организация", "");
            Иначе
                ЗаказПоставщику.Вставить("Организация", Строка(Выборка.Организация));
            КонецЕсли;
            
            Если Выборка.Ответственный = Неопределено Тогда
                ЗаказПоставщику.Вставить("Ответственный", "");
            Иначе
                ЗаказПоставщику.Вставить("Ответственный", Строка(Выборка.Ответственный));
            КонецЕсли;
            
            Если Выборка.Сумма = Неопределено Тогда
                ЗаказПоставщику.Вставить("Сумма", "");
            Иначе
                ЗаказПоставщику.Вставить("Сумма", Число(Выборка.Сумма));
            КонецЕсли;
            
            Если ПолныйРежим Тогда
                ОбъектЗаказ = Выборка.Ссылка.ПолучитьОбъект();
                
                Товары = Новый Массив;
                Для Каждого СтрокаТЧ Из ОбъектЗаказ.Товары Цикл
                    Стр = Новый Структура;
                    Стр.Вставить("НомерСтроки", Строка(СтрокаТЧ.НомерСтроки));
                    Стр.Вставить("Номенклатура", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Наименование), ""));
                    Стр.Вставить("Код", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Код), ""));
                    Стр.Вставить("ХарактеристикаНоменклатуры", ?(ЗначениеЗаполнено(СтрокаТЧ.ХарактеристикаНоменклатуры), Строка(СтрокаТЧ.ХарактеристикаНоменклатуры.Наименование), ""));
                    Стр.Вставить("ЕдиницаИзмерения", ?(ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения), Строка(СтрокаТЧ.ЕдиницаИзмерения.Наименование), ""));
                    Стр.Вставить("ЕдиницаИзмеренияМест", ?(ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмеренияМест), Строка(СтрокаТЧ.ЕдиницаИзмеренияМест.Наименование), ""));
                    Стр.Вставить("Количество", СтрокаТЧ.Количество);
                    Стр.Вставить("КоличествоМест", СтрокаТЧ.КоличествоМест);
                    Стр.Вставить("Коэффициент", СтрокаТЧ.Коэффициент);
                    Стр.Вставить("Цена", СтрокаТЧ.Цена);
                    Стр.Вставить("Сумма", СтрокаТЧ.Сумма);
                    Стр.Вставить("СтавкаНДС", Строка(СтрокаТЧ.СтавкаНДС));
                    Стр.Вставить("СуммаНДС", СтрокаТЧ.СуммаНДС);
                    Стр.Вставить("ПлановаяСебестоимость", СтрокаТЧ.ПлановаяСебестоимость);
                    Товары.Добавить(Стр);
                КонецЦикла;
                
                Услуги = Новый Массив;
                Для Каждого СтрокаТЧ Из ОбъектЗаказ.Услуги Цикл
                    Стр = Новый Структура;
                    Стр.Вставить("НомерСтроки", Строка(СтрокаТЧ.НомерСтроки));
                    Стр.Вставить("Содержание", Строка(СтрокаТЧ.Содержание));
                    Стр.Вставить("Номенклатура", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Наименование), ""));
                    Стр.Вставить("Код", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Код), ""));
                    Стр.Вставить("Количество", СтрокаТЧ.Количество);
                    Стр.Вставить("Цена", СтрокаТЧ.Цена);
                    Стр.Вставить("Сумма", СтрокаТЧ.Сумма);
                    Стр.Вставить("СтавкаНДС", Строка(СтрокаТЧ.СтавкаНДС));
                    Стр.Вставить("СуммаНДС", СтрокаТЧ.СуммаНДС);
                    Услуги.Добавить(Стр);
                КонецЦикла;
                
                Оборудование = Новый Массив;
                Для Каждого СтрокаТЧ Из ОбъектЗаказ.Оборудование Цикл
                    Стр = Новый Структура;
                    Стр.Вставить("НомерСтроки", Строка(СтрокаТЧ.НомерСтроки));
                    Стр.Вставить("Номенклатура", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Наименование), ""));
                    Стр.Вставить("Код", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Код), ""));
                    Стр.Вставить("ХарактеристикаНоменклатуры", ?(ЗначениеЗаполнено(СтрокаТЧ.ХарактеристикаНоменклатуры), Строка(СтрокаТЧ.ХарактеристикаНоменклатуры.Наименование), ""));
                    Стр.Вставить("ЕдиницаИзмерения", ?(ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения), Строка(СтрокаТЧ.ЕдиницаИзмерения.Наименование), ""));
                    Стр.Вставить("ЕдиницаИзмеренияМест", ?(ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмеренияМест), Строка(СтрокаТЧ.ЕдиницаИзмеренияМест.Наименование), ""));
                    Стр.Вставить("Количество", СтрокаТЧ.Количество);
                    Стр.Вставить("КоличествоМест", СтрокаТЧ.КоличествоМест);
                    Стр.Вставить("Коэффициент", СтрокаТЧ.Коэффициент);
                    Стр.Вставить("Цена", СтрокаТЧ.Цена);
                    Стр.Вставить("Сумма", СтрокаТЧ.Сумма);
                    Стр.Вставить("СтавкаНДС", Строка(СтрокаТЧ.СтавкаНДС));
                    Оборудование.Добавить(Стр);
                КонецЦикла;
                
                Материалы = Новый Массив;
                Для Каждого СтрокаТЧ Из ОбъектЗаказ.Материалы Цикл
                    Стр = Новый Структура;
                    Стр.Вставить("НомерСтроки", Строка(СтрокаТЧ.НомерСтроки));
                    Стр.Вставить("Номенклатура", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Наименование), ""));
                    Стр.Вставить("Код", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Код), ""));
                    Стр.Вставить("ХарактеристикаНоменклатуры", ?(ЗначениеЗаполнено(СтрокаТЧ.ХарактеристикаНоменклатуры), Строка(СтрокаТЧ.ХарактеристикаНоменклатуры.Наименование), ""));
                    Стр.Вставить("ЕдиницаИзмерения", ?(ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмерения), Строка(СтрокаТЧ.ЕдиницаИзмерения.Наименование), ""));
                    Стр.Вставить("ЕдиницаИзмеренияМест", ?(ЗначениеЗаполнено(СтрокаТЧ.ЕдиницаИзмеренияМест), Строка(СтрокаТЧ.ЕдиницаИзмеренияМест.Наименование), ""));
                    Стр.Вставить("Количество", СтрокаТЧ.Количество);
                    Стр.Вставить("КоличествоМест", СтрокаТЧ.КоличествоМест);
                    Стр.Вставить("Коэффициент", СтрокаТЧ.Коэффициент);
                    Стр.Вставить("Цена", СтрокаТЧ.Цена);
                    Стр.Вставить("Сумма", СтрокаТЧ.Сумма);
                    Материалы.Добавить(Стр);
                КонецЦикла;
                
                ВозвратнаяТара = Новый Массив;
                Для Каждого СтрокаТЧ Из ОбъектЗаказ.ВозвратнаяТара Цикл
                    Стр = Новый Структура;
                    Стр.Вставить("НомерСтроки", Строка(СтрокаТЧ.НомерСтроки));
                    Стр.Вставить("Номенклатура", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Наименование), ""));
                    Стр.Вставить("Код", ?(ЗначениеЗаполнено(СтрокаТЧ.Номенклатура), Строка(СтрокаТЧ.Номенклатура.Код), ""));
                    Стр.Вставить("Количество", СтрокаТЧ.Количество);
                    Стр.Вставить("Цена", СтрокаТЧ.Цена);
                    Стр.Вставить("Сумма", СтрокаТЧ.Сумма);
                    ВозвратнаяТара.Добавить(Стр);
                КонецЦикла;
                
                ЗаказПоставщику.Вставить("Товары", Товары);
                ЗаказПоставщику.Вставить("Услуги", Услуги);
                ЗаказПоставщику.Вставить("Оборудование", Оборудование);
                ЗаказПоставщику.Вставить("Материалы", Материалы);
                ЗаказПоставщику.Вставить("ВозвратнаяТара", ВозвратнаяТара);
                
                // Комментарий из шапки документа
                Если ОбъектЗаказ.Комментарий = Неопределено Тогда
                    ЗаказПоставщику.Вставить("Комментарий", "");
                Иначе
                    ЗаказПоставщику.Вставить("Комментарий", Строка(ОбъектЗаказ.Комментарий));
                КонецЕсли;
            КонецЕсли;
            
            МассивЗаказовПоставщикам.Добавить(ЗаказПоставщику);
        КонецЦикла;

        JSON = Новый ЗаписьJSON;
        JSON.УстановитьСтроку();
        ЗаписатьJSON(JSON, МассивЗаказовПоставщикам);
        JSONСтрока = JSON.Закрыть();

        Ответ = Новый HTTPСервисОтвет(200);
        Ответ.Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
        Ответ.УстановитьТелоИзСтроки(JSONСтрока);
        Возврат Ответ;
    Исключение
        ОписаниеОшибки = ОписаниеОшибки();
        Ответ = Новый HTTPСервисОтвет(500);
        Ответ.Заголовки.Вставить("Content-Type", "text/plain; charset=utf-8");
        Ответ.УстановитьТелоИзСтроки(ОписаниеОшибки);
        Возврат Ответ;
    КонецПопытки;
КонецФункции                                
